:: ka10_test.ini
:: This script will run the available CPU instruction and core
:: functional diagnostics for the KA10 simulator.
::
:: Default output summarizes success or failure.
:: if the script is invoked with -v as a parameter, verbose 
:: diagnostic output will be produced.
::
:: The related diagnostic files must be located in the same directory 
:: as this procedure.
::
cd %~p0
set env DIAG_QUIET_MODE=0
if ("%1" == "-v") set console notelnet
else set -qu console telnet=65432,telnet=buffered; set env -a DIAG_QUIET_MODE=1
goto DIAG_%SIM_BIN_NAME%

:DIAG_PDP10-KA
echo Booting paper tape.
at ptr diagnostics.rim
b ptr
if (PC != 100) echof "\r\n*** FAILED - %SIM_NAME%\n"; exit 1
else           echof "\r\n*** PASSED - %SIM_NAME%\n"; exit 0
